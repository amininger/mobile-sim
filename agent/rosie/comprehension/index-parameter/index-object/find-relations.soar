sp {index-object*propose*find-relations
   (state <s> ^name index-object
              ^object-to-index.object.lexical.relation <rel>
              ^object-to-index.matches.match <m>
             -^found-relation.<rel> <m>)
   (<m> ^id <id>)
   (<rel> ^word <prep>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^prep <prep>
        ^obj <id>
        ^ind 1
        ^relation <rel>
        ^match <m>)
}

sp {index-object*apply*find-relations*mark*found
   (state <s> ^name index-object
              ^operator <o>
              ^relationships <rels>)
   (<o> ^name find-relations
        ^relation <rel>
        ^match <m>)
   (<rel> ^word <prep>)
   (<m> ^id <id>)
   (<rels> ^preposition <prep>
        	^with <id>)
-->
   (<s> ^found-relation.<rel> <m>)
}

sp {index-object*apply*find-relations*remove*match
   (state <s> ^name index-object
              ^operator <o>
              ^object-to-index.object.indexable <i2>
              ^object-to-index.matches <m>
              ^relationships <rels>)
   (<o> ^name find-relations
        ^relation <rel>
        ^match <match>)
   (<m> ^match <match>)
   (<match> ^id <id>)
   (<i2> ^lexical <obj2>)
   (<rel> ^word <prep>
          ^p2.object <obj2>)
   (<rels> ^preposition <prep>
        	^with <id>)
   -{(<rels> ^relations.obj <id2>)
     (<i2> ^result.id <id2>)}
-->
   (<m> ^match <match> -)
}
           