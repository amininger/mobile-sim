sp {index-object*propose*match-relation
   (state <s> ^name index-object
              ^object-to-index.object.lexical.relation <rel>
              ^object-to-index.matches.finished true
             -^matched-relation <rel>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name match-relation
        ^relation <rel>)
}

sp {index-object*elaborate*match-relation*rel-object
   (state <s> ^name index-object
              ^operator <o> +
              ^object-to-index.object.indexable <i2>)
   (<o> ^name match-relation
        ^relation.p2.object <obj2>)
   (<i2> ^lexical <obj2>
         ^result <res>)
-->
   (<o> ^rel-object <res>)
}

sp {index-object*match*elaborate*valid-match 
   (state <s> ^name index-object
              ^operator <o> +
              ^object-to-index.matches.match <match>
              ^topstate.svs.command.extract <filter>)
   (<o> ^name match-relation
        ^relation.word <prep>
        ^rel-object.belief-obj.id <id2>)
   (<match> ^belief-obj.id <id1>)
   (<filter> ^mark <prep>
             ^a.id <id1>
             ^result.record.value <id2>)
-->
   (<o> ^valid-match <match>)
}

sp {index-object*apply*match-relation*mark*matched
   (state <s> ^name index-object
              ^operator <o>)
   (<o> ^name match-relation
        ^relation <rel>)
-->
   (<s> ^matched-relation <rel>)
}

# Any match that is not valid (does not satisfy the relation) is removed
sp {index-object*apply*match-relation*eliminate-match
   (state <s> ^name index-object
              ^operator <o>
              ^object-to-index.matches <m>)
   (<o> ^name match-relation
       -^valid-match <match>)
   (<m> ^match <match>)
-->
   (<m> ^match <match> -)
}