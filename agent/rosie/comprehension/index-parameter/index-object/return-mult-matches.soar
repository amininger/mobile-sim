#################################################
#
# return-mult-matches
#   Multiple objects have matched, return all, use interaction, 
#
##################################################

sp {index-object*propose*return-mult-matches
   (state <s> ^name index-object
              ^object-to-index.matches <m>)
   (<m> ^finished true
        ^mult-matches true
        ^match <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name return-mult-matches
        ^match <obj>)
}

# Prefer objects higher on the hierarchy (e.g. object referred to recently)
sp {index-object*return-mult-matches*prefer*lower-rank
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +
              ^object-to-index.candidates <c>)
   (<o1> ^name return-mult-matches
         ^match <obj1>)
   (<o2> ^name return-mult-matches
         ^match <obj2>)
   (<c> ^candidate <c1>
        ^candidate <c2>)
   (<c1> ^object <obj1>
         ^pref <p1>)
   (<c2> ^object <obj2>
         ^pref > <p1>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*apply*return-mult-matches*create*result-info
   (state <s> ^name index-object
              ^operator.name return-mult-matches
              ^object-to-index.object <obj>)
-->
   (<obj> ^result-info <info>)
}

sp {index-object*apply*return-mult-matches*copy*match
   (state <s> ^name index-object
              ^operator <o>
              ^object-to-index.object <obj>
              ^object-to-index.candidates.candidate <cand>)
   (<o> ^name return-mult-matches
        ^match <m>)
   (<obj> ^result-info <info>)
   (<cand> ^object <m>
           ^pref <pref>)
-->
   (<obj> ^result <m>)
   (<info> ^score <score>)
   (<score> ^result <m>
            ^value <pref>)
}

##########################################
# Case 1: return-all = true
##########################################

# No need to tie, just pick one operator then copy all matches
sp {index-object*apply*return-mult-matches*return-all
   (state <s> ^name index-object
              ^operator <o> +
              ^index-info.return-all true
              ^object-to-index.matches <m>)
   (<o> ^name return-mult-matches
        ^match <obj>)
-->
   (<s> ^operator <o> =)
}

sp {index-object*apply*return-mult-matches*return-all*true
   (state <s> ^name index-object
              ^operator.name return-mult-matches
             -^operator.match <m>
              ^object-to-index <oti>
              ^index-info.return-all true)
   (<oti> ^object <obj>
          ^matches.match <m>
          ^candidates.candidate <c>)
   (<c> ^object <m>
        ^pref <pref>)
   (<obj> ^result-info <info>)
-->
   (<obj> ^result <m>)
   (<info> ^score <score>)
   (<score> ^result <m>
            ^value <pref>)
}

##########################################
# Case 2: return-all = false and use-interaction = false
##########################################

# We just give indifferent preferences, so one is returned at random
sp {index-object*apply*return-mult-matches*no-interaction
   (state <s> ^name index-object
              ^operator <o> +
              ^index-info <info>)
   (<o> ^name return-mult-matches)
   (<info> ^return-all false
           ^use-interaction false)
-->
   (<s> ^operator <o> =)
}

#####################################################
# Case 3: return-all = false and use-interaction = true
#####################################################

# A tie occurs, so we ask a which-question to the user

sp {index-object*elaborate*mult-matches-tie
   (state <s> ^superstate.name index-object
              ^impasse tie
              ^choices multiple
              ^item.name return-mult-matches)
-->
   (<s> ^name mult-matches-tie)
}

sp {index-object*propose*return-mult-matches-tie
   (state <s> ^name mult-matches-tie)
-->
   (<s> ^operator <o> + >)
   (<o> ^name return-mult-matches-tie)
}

sp {index-object*apply*return-mult-matches-tie*report*index-object-tie
   (state <s> ^name mult-matches-tie
              ^operator.name return-mult-matches-tie
              ^superstate.object-to-index.object <obj>
              ^topstate.interaction.status <status>)
-->
   (<status> ^failure <f>)
   (<f> ^type index-object-tie
        ^target-object <obj>)
}
