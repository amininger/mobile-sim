# elaborations.soar

sp {index-object*elaborate*object-to-index
   (state <s> ^name index-object
              ^superstate.operator.object-param <obj>)
-->
   (<s> ^object-to-index <oti>)
   (<oti> ^object <obj>
          ^candidates <c>
          ^matches <m>)
}

sp {index-object*elaborate*word
   (state <s> ^name index-object
              ^object-to-index <oti>)
   (<oti> ^object.indexable.result <word-rep>)
   (<word-rep> -^sense.property.name generic)
-->
   (<oti> ^word <word-rep>)
}

####################################################
#
# Parameters (index-info)
#    Controls how the indexing process happens
#
######################################################

sp {index-object*elaborate*parameters*wme
   (state <s> ^name index-object)
-->
   (<s> ^index-info <p>)
}

# Elaborations for copying parameters from the top state
sp {index-object*elaborate*boolean*parameter*copy
   (state <s> ^name index-object
              ^object-to-index.object.index-info <info>
              ^index-info <p>)
   (<info> ^{<par> << use-interaction return-all infer-match >>} <val>)
-->
   (<p> ^<par> <val>)
}

### PARAMETER-DEFAULT ###

# use-interaction: false
sp {index-object*elaborate*use-interaction*default
   (state <s> ^name index-object
              -^object-to-index.object.index-info.use-interaction
              ^index-info <p>)
-->
   (<p> ^use-interaction false)
}

# return-all: false
sp {index-object*elaborate*return-all*default
   (state <s> ^name index-object
              -^object-to-index.object.index-info.return-all
              ^index-info <p>)
-->
   (<p> ^return-all false)
}

# infer-match: false
sp {index-object*elaborate*infer-match*default
   (state <s> ^name index-object
             -^object-to-index.object.index-info.infer-match
              ^index-info <p>)
-->
   (<p> ^infer-match false)
}

########################################
#
# elaborate number of matches
#
########################################
sp {index-object*elaborate*no-matches
   (state <s> ^name index-object
              ^object-to-index.matches <m>)
   (<m> ^finished true
       -^match)
-->
   (<m> ^no-matches true)
}

sp {index-object*elaborate*one-match
   (state <s> ^name index-object
              ^object-to-index.matches <m>)
   (<m> ^finished true
        ^match <m1>
       -^match <> <m1>)
-->
   (<m> ^one-match true)
}

sp {index-object*elaborate*mult-matches
   (state <s> ^name index-object
              ^object-to-index.matches <m>)
   (<m> ^finished true
        ^match <m1>
        ^match <> <m1>)
-->
   (<m> ^mult-matches true)
}