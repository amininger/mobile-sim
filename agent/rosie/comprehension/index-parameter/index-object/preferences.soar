# 1: attend-to-scene-changes*create-delete
sp {index-object*greater-preferences*attend-to-scene-changes*create-delete
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-changes
         ^type create-delete)
   (<o2> ^name << add-candidates find-matches find-relations >>)
-->
   (<s> ^operator <o1> > <o2>)
}

# 2: add-candidates
sp {index-object*greater-preferences*add-candidates
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates)
   (<o2> ^name << find-matches find-relations >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*add-candidates*prefer*focus*over*activation*all
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates
         ^candidate-type focus)
   (<o2> ^name add-candidates
         ^candidate-type << activation all >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*add-candidates*prefer*activation*over*all
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name add-candidates
         ^candidate-type activation)
   (<o2> ^name add-candidates
         ^candidate-type all)
-->
   (<s> ^operator <o1> > <o2>)
}

# 3: attend-to-scene-changes*candidates
sp {index-object*greater-preferences*attend-to-scene-changes*candidates
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-changes
         ^type candidates)
   (<o2> ^name << find-matches find-relations >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*less-preferences*attend-to-scene-changes*candidates
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << add-candidates >>)
   (<o2> ^name attend-to-scene-changes
         ^type candidates)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {index-object*attend-to-scene-changes*prefer*create-delete*over*candidates
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-changes
         ^type create-delete)
   (<o2> ^name attend-to-scene-changes
         ^type candidates)
-->
   (<s> ^operator <o1> > <o2>)
}


# 4: find-matches
sp {index-object*greater-preferences*find-matches
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name find-matches)
   (<o2> ^name << find-relations >>)
-->
   (<s> ^operator <o1> > <o2>)
}

# 5: find-relations
sp {index-object*greater-preferences*find-relations
   (state <s> ^name index-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name find-relations)
   (<o2> ^name << return-no-matches return-one-match return-mult-matches >>)
-->
   (<s> ^operator <o1> > <o2>)
}

