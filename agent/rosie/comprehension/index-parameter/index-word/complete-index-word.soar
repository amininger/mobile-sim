# Finish the lookup
sp {index-word*propose*complete-index-word
   (state <s> ^name index-word
              ^retrieved-word <word>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-index-word
        ^category smem)
}

# Copy the result structure to the superstate
sp {index-word*apply*complete-index-word*success
   (state <s> ^name index-word
              ^operator.name complete-index-word
              ^word-param <target>
              ^retrieved-word <word>)
-->
   (<target> ^result <word>)
}

# Updates any objects with the word, for convenience
sp {index-word*apply*complete-index-word*annotate*object
   (state <s> ^name index-word
              ^operator.name complete-index-word
              ^retrieved-word <w>
              ^topstate.object.property <prop>)
   (<w> ^word <word>
           ^sense <sense>)
   (<sense> ^label <label>
            ^property.name <prop-name>)
   (<prop> ^name <prop-name>
           ^value <val>)
   (<val> ^label <label>
         -^word <word>)
-->
   (<val> ^word <word>)
}
   