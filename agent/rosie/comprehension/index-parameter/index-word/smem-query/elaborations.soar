# Parameters:

sp {smem-query*elaborate*copy*parameters
   (state <s> ^name smem-query
              ^superstate.operator.{<param> << return-all >>} <val>)
-->
   (<s> ^<param> <val>)
}

# Parameter: return-all << true false >>
#   If false, returns the first result based on the bias
#   If true, returns all found structures matching the given query
#   Default: false
sp {smem-query*elaborate*return-all*default*false
   (state <s> ^name smem-query
             -^superstate.operator.return-all)
-->
   (<s> ^return-all false)
}

# Elaborations: Creates the initial retrieval-count at 0

sp {smem-query*elaborate*default*count
   (state <s> ^name smem-query
             -^retrieval-count > 0)
-->
   (<s> ^retrieval-count 0)
}

sp {smem-query*elaborate*query
   (state <s> ^name smem-query
              ^superstate.operator.query <q>)
-->
   (<s> ^query <q>)
}

sp {smem-query*elaborate*prohibitions
   (state <s> ^name smem-query
              ^smem-query-result <res> 
              ^smem.command <cmd>)
-->
   (<cmd> ^prohibit <res>)
}