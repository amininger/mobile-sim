sp {generate*operator*name
   (state <s> ^name generate-indexed-operator
   	      ^procedural-id <pid>)
   (<pid> ^op_name <op-name>)
-->
   (<s> ^indexed-operator <op>
   	^_metadata <met>)
   (<op> ^name <op-name>)
   (<op> ^_metadata <met>)
 }

sp {generate*operator*direct-object*arguments*level1
   (state <s> ^name generate-indexed-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^indexable-parameter <ip>
	      ^indexed-operator <op>
	      ^_metadata <met>)
   (<ip> ^direct-object <sub-ip>)
   (<sub-ip> ^result <obj>)
   (<lid> ^direct-object <lobj>)
   (<pid> ^<arg> <lobj>)
-->
   (<op> ^<arg> <obj>)
}

sp {generate*operator*pp-object*arguents*level1
   (state <s> ^name generate-indexed-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^indexable-parameter <ip>
	      ^indexed-operator <op>)
   (<ip> ^{<iparg> << pp-object state-object >>}.result <sub-ip>)
   (<sub-ip> ^object <obj>)
   (<pid> ^<arg> <val>)
   (<val> ^argument <objid>)
   (<lid> ^<iparg>.object <objid>)
-->
   (<op> ^<arg> <new>)
   (<s> ^pairing <pair>)
   (<pair> ^<iparg> <sub-ip>
   	   ^procedural-id-node <val>
	   ^object <obj>
	   ^indexed-operator-arg <new>)
}

sp {generate*operator*pp-object*argument
   (state <s> ^name generate-indexed-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^indexed-operator.<arg> <new>
	      ^pairing <pair>
	      ^_metadata <met>)
   (<pair> ^{<iparg> << pp-object state-object >>} <res>
   	   ^procedural-id-node <val>
	   ^object <obj>
	   ^indexed-operator-arg <new>)
   (<res> ^object <obj>)
   (<lid> ^<iparg> <ppobj>)
   (<ppobj> ^object <objid>)
   (<pid> ^<arg> <val>)
   (<val> ^argument <objid>)
-->
   (<new> ^argument <obj>)
}


sp {generate*operator*pp-object*relation
   (state <s> ^name generate-indexed-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^indexed-operator.<arg> <new>
	      ^pairing <pair>)
   (<pair> ^pp-object <res>
   	   ^procedural-id-node <val>
	   ^object <obj>
	   ^indexed-operator-arg <new>)
   (<res> ^relation <rel>
   	  ^object <obj>)
   (<lid> ^pp-object <ppobj>)
   (<ppobj> ^preposition <relid>)
   (<pid> ^<arg> <val>)
   (<val> ^relation <relid>)
-->
   (<new> ^relation <nrel>)
   (<nrel> ^smem-rep <rel>)
}

sp {generate*operator*state-object*state
   (state <s> ^name generate-indexed-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^indexed-operator.<arg> <new>
	      ^pairing <pair>)
   (<pair> ^state-object <res>
   	   ^procedural-id-node <val>
	   ^object <obj>
	   ^indexed-operator-arg <new>)
   (<res> ^state <rel>)
   (<lid> ^state-object <ppobj>)
   (<ppobj> ^state <relid>)
   (<pid> ^<arg> <val>)
   (<val> ^state <relid>)
-->
   (<new> ^state <rel>)
}


sp {generate-operator*copy*metadat
   (state <s> ^name generate-indexed-operator
   	      ^indexed-operator <op>
	      ^map-id <map>)
   (<op> ^_metadata <met>)
-->
   (<met> ^map-id <map>)
}