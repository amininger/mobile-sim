

############################################################
#
# learn-word
#
############################################################

sp {learning-event*propose*learn-word
   (state <s> ^name learning-event
              ^purpose <p>)
   (<p> ^type learn-word
        ^parameters.word <word>)
-->
   (<s> ^operator <o> +)
   (<o> ^name learn-word
        ^word <word>
        ^category smem)
}

############################################################
#
# train-on-object
#
############################################################

sp {learning-event*propose*train-on-object
   (state <s> ^name learning-event
              ^purpose <p>)
   (<p> ^type train-on-object
        ^parameters.indexable <obj>)
   (<obj> ^type object)
-->
   (<s> ^operator <o> +)
   (<o> ^name train-on-object
        ^object <obj>)
}

sp {learning-event*elaborate*train-on-object*word
   (state <s> ^name learning-event
              ^operator <o> +
              ^purpose.parameters.indexable <word>)
   (<o> ^name train-on-object)
   (<word> ^type word)
-->
   (<o> ^word <word>)
}

############################################################
#
# learn-preposition
#
############################################################
#JK
sp {learning-event*propose*learn-preposition
   (state <s> ^name learning-event
              ^purpose <p>)
   (<p> ^type teach-preposition
        ^parameters <param>)
   (<param> ^indexable <i1>
             ^indexable <i2>
             ^object1 <lex1>
             ^object2 <lex2>
             ^prep <prep>)
   (<i1> ^lexical <lex1> ^result <obja>)
   (<i2> ^lexical <lex2> ^result <objb>)
-->
   (<s> ^operator <o> +)
   (<o> ^name learn-preposition
        ^message <m>)
   (<m> ^fields <f> ^type learn-preposition)
   (<f> ^objects <object>)
   (<object> ^obj1 <obja>
             ^obj2 <objb>
             ^type <prep>)
}