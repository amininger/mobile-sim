## The wait operator which gets selected when an action is executing 
sp {non-atomic*propose*wait
    (state <s> ^choices none ^kind non-atomic
               ^io.input-link.time.steps <t>) 
# AM: Changed to give enough time to apply the waiting rule
    -->
    (<s> ^operator <q> +)
    (<q> ^name wait +)
}

sp {state-no-change*propose*wait
   (state <s> ^attribute state
              ^choices none
              ^io.input-link.time.steps <t>)
-->
   (<s> ^operator <o> +)
   (<o> ^name wait)
}

# AM: added to let the chat window whether the agent is idle
sp {apply*wait*output-link*waiting
   (state <s> ^operator <o>
              ^io.input-link.time.steps <t>
              ^io.output-link <ol>)
   (<o> ^name wait)
-->
   (<ol> ^waiting <t>)
}

sp {apply*attend-to-scene-change*output-link*waiting
   (state <s> ^operator.name attend-to-scene-change
              ^io.output-link <ol>
              ^io.input-link.time.steps <t>)
-->
   (<ol> ^waiting <t>)
}

sp {apply*operator*remove*old*waiting
   (state <s> ^operator <o>
              ^io.input-link.time.steps <t>
              ^io.output-link <ol>)
   (<ol> ^waiting {<old-t> < <t>})
-->
   (<ol> ^waiting <old-t> -)
}

sp {non-atomic*prefer*wait*primitive-actions
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name wait)
   (<op2> ^category primitive-action)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {non-atomic*prefer*svs*wait
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name wait)
   (<op2> ^category svs)
-->
   (<s> ^operator <op2> > <op1>)
}