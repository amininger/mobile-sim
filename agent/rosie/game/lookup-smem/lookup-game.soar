#sp {lookup-game*query
#   (state <s> ^name lookup-game
#              ^superstate.operator.game-name <name>
#              ^smem.command <sc>)
#-->
#   (<sc> ^query <q>)
#   (<s> ^found-game <game>)
#   (<game> ^name <name>)
#   (<q> ^name <name>)
#}
sp {lookup-game*propose*lookup-general
   (state <s> ^name lookup-game
              ^superstate.operator.game-name <name>
             -^superstate.operator.full no
             -^failed
             -^retrieved)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-general
        ^category svs
        ^game-name <name>)
}

sp {lookup-game*query
   (state <s> ^name lookup-game
              ^superstate.operator.game-name <name>
              ^superstate.operator.full no
              ^smem.command <sc>)
-->
   (<sc> ^query <q>)
   (<q> ^game-name <name>)
}

sp {lookup-game*query*success
   (state <s> ^name lookup-game
              ^superstate <ss>
              ^superstate.operator.game-name <name>
              ^superstate.operator.full no
              ^smem.result.success <cue>
              ^smem.result <res>)
   (<res> ^retrieved <ret>)
-->
   (<ss> ^game <ret>)
}

sp {lookup-game*query*failure
   (state <s> ^name lookup-game
              ^superstate <ss>
              ^superstate.operator.game-name <name>
              ^superstate.operator.full no
              ^smem.result.failure <cue>)
-->
   (<ss> ^failed true)
}


sp {lookup-game*propose*complete-lookup-game
   (state <s> ^name lookup-game
             -^superstate.operator.full no
              ^superstate.operator.game-name <name>
              ^retrieved <ret>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-lookup-game
        ^category svs
        ^retrieved <ret>)
}

sp {lookup-game*propose*complete-lookup-game*failure
   (state <s> ^name lookup-game
              ^superstate.operator.game-name <name>
              ^failed true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-lookup-game
        ^category svs
        ^failed true)
}

sp {lookup-game*apply*complete-lookup-game
   (state <s> ^name lookup-game
              ^superstate.operator.game-name <name>
              ^retrieved <ret>
              ^superstate <ss>
              ^operator <o>)
   (<o> ^name complete-lookup-game
        ^retrieved <ret>)
-->
   (<ss> ^game <ret>)
}

sp {lookup-game*apply*complete-lookup-game*failure
   (state <s> ^name lookup-game
              ^superstate.operator.game-name <name>
              ^superstate <ss>
              ^failed true
              ^operator <o>)
   (<o> ^name complete-lookup-game
        ^failed true)
-->
   (<ss> ^failed true)
}