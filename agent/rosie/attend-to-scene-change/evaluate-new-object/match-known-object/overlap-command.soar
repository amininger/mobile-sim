sp {match-known-object*propose*overlap-command
   (state <s> ^name match-known-object
              ^monitor-obj.perception-obj.id <new-id>
              ^intersect-command.result {<bel-id> <> none}
             -^overlap-results.result.object-b <bel-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^category perception
        ^object-a <new-id>
        ^object-b <bel-id>)
}

sp {match-known-object*apply*overlap-command*copy-result
   (state <s> ^name match-known-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>)
   (<cmd> ^result <res>)
   (<res> ^overlap <val>)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {match-known-object*apply*overlap-command*debug-perception*report*overlap
   (state <s> ^name match-known-object
              ^operator.name overlap-command
              ^overlap-command.result <res>
              ^topstate.agent-params.debug-perception true)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 