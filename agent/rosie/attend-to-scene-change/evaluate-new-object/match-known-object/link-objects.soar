sp {match-known-object*propose*link-objects
   (state <s> ^name match-known-object
              ^monitor-obj.perception-obj.id <source>
              ^overlap-results.result <res>)
   (<res> ^object-b <dest>
          ^overlap <val>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name link-objects
        ^category perception
        ^dest-id <dest>
        ^source-id <source>
        ^overlap <val>)
}

sp {match-known-object*reject*not-enough-overlap
   (state <s> ^name match-known-object
              ^operator <o>
              ^contained-threshold <thresh>)
   (<o> ^name link-objects
        ^overlap < <thresh>)
-->
   (<s> ^operator <o> -)
}

sp {match-known-object*reject*better-candidate
   (state <s> ^name match-known-object
              ^operator <o>
              ^contained-threshold <thresh>
              ^topstate.scene-monitor.object <stale-obj>)
   (<o> ^name link-objects
        ^dest-id <stale-id>
        ^source-id <new-id>
        ^overlap < <thresh>)
   (<stale-obj> ^belief-obj.id <stale-id>
                ^differences.possible-candidates <cands>)
   (<cands> ^candidate <> <new-id>)
-->
   (<s> ^operator <o> -)
}

sp {match-known-object*prefer*link-objects*more*overlap
   (state <s> ^name match-known-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name link-objects
         ^overlap <val1>)
   (<o2> ^name link-objects
         ^overlap {<val2> < <val1>})
-->
   (<s> ^operator <o1> > <o2>)
}

sp {match-known-object*apply*link-objects
   (state <s> ^name match-known-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-id <source>
         ^dest-id <dest>)
}

sp {match-known-object*apply*link-objects*monitor*perception
   (state <s> ^name match-known-object
              ^operator <o>
              ^topstate.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (write |Linking object | <source> | to | <dest>)
}
        