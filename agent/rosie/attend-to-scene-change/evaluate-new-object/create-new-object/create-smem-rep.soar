sp {create-new-object*propose*create-smem-rep
   (state <s> ^name create-new-object
              ^new-object <obj>)
   (<obj> -^smem-rep)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-smem-rep
        ^category perception)
}

sp {create-new-object*apply*create-smem-rep*query*smem
   (state <s> ^name create-new-object
              ^operator.name create-smem-rep
              ^new-object.id <id>
              ^smem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^id <id>
        ^wme-type object)
}

sp {create-new-object*apply*create-smem-rep*success*copy-rep
   (state <s> ^name create-new-object
              ^operator.name create-smem-rep
              ^smem <smem>
              ^new-object <obj>)
   (<smem> ^result <res>
           ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <ret>)
   (<cmd> ^query <q>)
-->
   (<obj> ^smem-rep <ret>)
   (<cmd> ^query <q> -)
}

sp {create-new-object*apply*create-smem-rep*failure*create-rep
   (state <s> ^name create-new-object
              ^operator.name create-smem-rep
              ^smem <smem>
              ^new-object <obj>)
   (<smem> ^result.failure <q>
           ^command <cmd>)
   (<cmd> ^query <q>)
   (<obj> ^id <id>)
-->
   (<obj> ^smem-rep <rep>)
   (<rep> ^id <id>
          ^wme-type object)
   (<cmd> ^query <q> -)
}
                   