sp {update-object*propose*check-for-occlusion
   (state <s> ^name update-object
              ^monitor-obj <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name check-for-occlusion
        ^category perception
        ^monitor-obj <obj>
        ^report-to-superstate true
        ^report-to-topstate false)
}

sp {update-object*apply*check-for-occlusion*update*time
   (state <s> ^name update-object
              ^operator.name check-for-occlusion
              ^object-occluded <bool>
              ^monitor-obj.differences <diffs>
              ^io.input-link.time.seconds <t>
              ^update-object-period <period>)
   (<diffs> ^update-object-time <old-t>)
-->
   (<diffs> ^update-object-time <old-t> -
            ^update-object-time (+ <t> <period>))
}

sp {update-object*apply*check-for-occlusion*not-occluded*mark-for-copying
   (state <s> ^name update-object
              ^operator.name check-for-occlusion
              ^object-occluded false
              ^monitor-obj <obj>)
-->
   (<obj> ^mark-for-copying true)
}