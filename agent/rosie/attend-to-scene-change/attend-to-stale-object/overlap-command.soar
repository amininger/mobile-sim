sp {attend-to-stale-object*propose*overlap-command*contains
   (state <s> ^name attend-to-stale-object
              ^monitor-obj.belief-obj.id <stale-id>
              ^intersect-command.result {<perc-id> <> none}
             -^topstate.object.perception-obj.id <perc-id>
             -^overlap-results.result.object-a <perc-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^category perception
        ^object-a <perc-id>
        ^object-b <stale-id>)
}

sp {attend-to-stale-object*apply*overlap-command*copy-result
   (state <s> ^name attend-to-stale-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>)
   (<cmd> ^result <res>)
   (<res> ^overlap <> none)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {attend-to-stale-object*apply*overlap-command*debug-perception*report*overlap
   (state <s> ^name attend-to-stale-object
              ^operator.name overlap-command
              ^overlap-command.result <res>
              ^topstate.agent-params.debug-perception true)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 