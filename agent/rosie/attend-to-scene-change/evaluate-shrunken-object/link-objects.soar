sp {evaluate-shrunken-object*propose*link-objects
   (state <s> ^name evaluate-shrunken-object
              ^found-contained-fragment true
              ^monitor-obj.belief-obj.id <dest-id>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-objects
        ^category perception
        ^dest-id <dest-id>)
}

sp {evaluate-shrunken-object*elaborate*link-objects*source-id
   (state <s> ^name evaluate-shrunken-object
              ^operator <o> +
              ^contained-fragment <src-id>)
   (<o> ^name link-objects)
-->
   (<o> ^source-id <src-id>)
}

# Need to do all at once, otherwise only 1 fragment will be linked
sp {evaluate-shrunken-object*apply*link-objects
   (state <s> ^name evaluate-shrunken-object
              ^operator <o>
              ^io.output-link <ol>)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (<ol> ^modify-scene <ms>)
   (<ms> ^type link
         ^source-id <source>
         ^dest-id <dest>)
}

sp {evaluate-shrunken-object*apply*link-objects*monitor*perception
   (state <s> ^name evaluate-shrunken-object
              ^operator <o>
              ^topstate.agent-params.debug-perception true)
   (<o> ^name link-objects
        ^dest-id <dest>
        ^source-id <source>)
-->
   (write |Linking object | <source> | to | <dest> (crlf))
}
        