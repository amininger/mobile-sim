sp {check-clear-view*propose*complete-check-clear-view
   (state <s> ^name check-clear-view)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-check-clear-view
        ^category perception)
}

sp {check-clear-view*elaborate*complete-check-clear-view*clear-object-view*default
   (state <s> ^name check-clear-view
             -^clear-object-view
              ^operator <o> +)
   (<o> ^name complete-check-clear-view)
-->
   (<o> ^clear-object-view false)
}

sp {check-clear-view*elaborate*complete-check-clear-view*clear-object-view*copy
   (state <s> ^name check-clear-view
              ^clear-object-view <bool>
              ^operator <o> +)
   (<o> ^name complete-check-clear-view)
-->
   (<o> ^clear-object-view <bool>)
}

sp {check-clear-view*elaborate*complete-check-clear-view*best-pref
   (state <s> ^name check-clear-view
              ^clear-object-view false
              ^operator <o> +)
   (<o> ^name complete-check-clear-view)
-->
   (<s> ^operator <o> >)
}

sp {check-clear-view*prefer*others*over*complete
   (state <s> ^name check-clear-view
             -^clear-object-view false
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name <> complete-check-clear-view
         ^category perception)
   (<o2> ^name complete-check-clear-view)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {check-clear-view*apply*complete-check-clear-view*copy*result
   (state <s> ^name check-clear-view
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name complete-check-clear-view
        ^clear-object-view <bool>)
-->
   (<ss> ^clear-object-view <bool>)
}