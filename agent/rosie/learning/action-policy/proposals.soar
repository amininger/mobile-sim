### propose*composite*action
sp {action-policy-learning*propose*operator
   (state <s> ^name action-policy
   	      ^indexed-action <ia>)
   (<ia> ^name <name>
   	 ^_metadata <met>)
-->
   (<s> ^operator <new-op> + >)
   (<new-op> ^name <name>
   	     ^_metadata <met>
	     ^action-type composite)
}

### elaborate composite action arguments level 1
sp {action-policy-learning*elaborate*composite-action-operator*level1
   (state <s> ^name action-policy
   	      ^indexed-action <ia>
	      ^operator <op> +
	      ^object <obj>)
   (<op> ^name <name>)
   (<ia> ^name <name>
   	 ^<arg> <val>)
   (<val> ^id <id>)
   (<obj> ^id <id>)
-->
   (<op> ^<arg> <obj>)
}

sp {action-policy-learning*elaborate*composite-action-operator*level2*object
   (state <s> ^name action-policy
   	      ^indexed-action <ia>
	      ^operator <op> +
	      ^object <obj>)
   (<op> ^name <name>)
   (<ia> ^name <name>
   	 ^<arg>.argument <val>)
   (<val> ^id <id>)
   (<obj> ^id <id>)
-->
   (<op> ^<arg>.argument <obj>)
}

sp {action-policy-learning*elaborate*composite-action-operator*level2*relation
   (state <s> ^name action-policy
   	      ^indexed-action <ia>
	      ^operator <op> +
	      ^object <obj>)
   (<op> ^name <name>
   	 ^<arg> <karg>)
   (<karg> ^argument <valx>)
   (<ia> ^name <name>
   	 ^<arg> <nval>)
   (<nval> ^argument <valy>
   	   ^relation <rel>)
   (<valx> ^id <id>)
   (<valy> ^id <id>)
-->
   (<karg> ^relation <rel>)
}

