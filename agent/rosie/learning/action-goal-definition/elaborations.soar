sp {elaborate*purpose*action-goal-definition
   (state <s> ^name action-goal-definition
   	      ^superstate.operator.purpose <p>)
-->
   (<s> ^purpose <p>)
}


sp {elaborate*action-goal-definition*indexed-action*desired
   (state <s> ^name action-goal-definition
   	      ^purpose <p>
	      ^topstate.interaction.stack.segment.next <ns>)
  (<ns> ^purpose.parameters.indexable.verb <any-verb>)
  (<p> ^parameters.indexable.result <desired>)
-->
  (<ns> ^desired-achievement-goal <desired>)
  (<s> ^segment-desired <desired>)
}

sp {elaborate*instantiation*of*goal-definition*indexed-action
   (state <s> ^name goal-definition
   	      ^superstate.segment-desired <des>
	      ^pairing <pair>)
   (<pair> ^smem <nrel>
   	   ^wmem <rel>)
   (<des> ^<pred> <rel>)
-->
   (<rel> ^_instantiation-of <nrel>)
}

sp {elaborate*instantiation*of*default-relation*indexed-action
   (state <s> ^name learn-defaults
   	      ^superstate.segment-desired <des>
	      ^learn-default-for <f>)
   (<f> ^goal-predicate-example <rel>
   	^predicate-definition <def>)
   (<des> ^<pred> <rel>)
-->
   (<rel> ^_instantiation-of <def>)
}

sp {elaborate*instantiation*of*default-relation*indexed-action*successful-grounding
   (state <s> ^name learn-defaults
   	      ^superstate.segment-desired <des>
	      ^not-learn-default-for <f>)
   (<f> ^predicate-definition <def>
   	^predicate-grounding <pred>)
   (<pred> ^p1 <p1-obj>
   	   ^state <state>)
   (<des> ^<desired-predicate> <some-predicate>)
   (<some-predicate> ^p1 <p1-obj>
   		     ^state <state>)
-->
   (<some-predicate> ^_instantiation-of <def>)
}