sp {learn-preposition*extract-relations*axis*result
   (state <s> ^name learn-preposition
              ^superstate.operator.message.fields.objects <objs>
   			  ^topstate <sss>
              ^topstate.axis-filters <af>
              ^label-svs <l>)
   (<sss> ^svs.command <cmd>)
   (<sss> ^svs.spatial-scene <sc>)
   (<af> ^filters <fil>)
   (<fil> ^filter <t>
          ^axis <axis>)
   (<objs> ^type <type>
           ^obj1 <oa>
           ^obj2 <ob>)
   (<ob> ^id <objb>)
   (<oa> ^id <obja>)
   (<s> ^predicate <p1>)
   (<p1> ^name <type>)
   (<cmd> ^extract_once <E>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<E> ^type <t> ^result.record <rec>)
   #(<rec> ^value t ^params <A>)
   (<rec> ^value <objb1> ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)   
-->
   (<l> ^result <r>)
   (<r> ^axis <axis> ^type <t>)
}

sp {learn-preposition*extract-relations*extract_once*distance
   (state <s> ^name learn-preposition
   			  ^topstate <sss>
              ^superstate.operator.message.fields.objects <objs>
              ^label-svs <l>)
   (<sss> ^svs.command <cmd>)
   (<sss> ^svs.spatial-scene <sc>)
   (<objs> ^type <type>
           ^obj1 <oa>
           ^obj2 <ob>)
   (<ob> ^id <objb>)
   (<oa> ^id <obja>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<l> ^result <r>)
   (<r> ^axis <axis>)
-->
   (<cmd> ^extract_once <P11>)
   (<P11> ^a <N7> ^b <N8> ^type distance_xyz ^axis <axis>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}

sp {learn-preposition*extract-relations*attach-distance
   (state <s> ^name learn-preposition
   			  ^topstate <sss>
              ^label-svs <l>)
   (<sss> ^svs.command <cmd>)
   (<cmd> ^extract_once <E>)
   (<E> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> ^value <dist>)
   (<l> ^result <r>)
   (<r> ^axis <axis>)
-->
   (<r> ^dist <dist>)
}

sp {learn-preposition*extract-relations*create-new-preposition
   (state <s> ^name learn-preposition
              ^superstate.operator.message.fields.objects <objs>
             -^stat old
              ^stat default)
   (<objs> ^type <type>)
-->
   (<s> ^label-svs <l>)
   (<s> ^created true)
   (<s> ^predicate <p1>)
   (<p1> ^name <type> ^all-aligned false) 
}

sp {learn-preposition*extract-relations*update-preposition
   (state <s> ^name learn-preposition
              ^superstate.operator.message.fields.objects <objs>
              ^stat old
             -^stat default)
   (<objs> ^type <type>)
-->
   (<s> ^label-svs <l>)
}

sp {learn-preposition*extract-relations*propose*lookup-preposition
   (state <s> ^name learn-preposition
             -^found-prep
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <type>)
}

sp {learn-preposition*extract-relations*preposition-list
   (state <s> ^name learn-preposition
              ^topstate.prepositions <p>
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>)
-->
   (<s> ^prep-list <p>)
}

sp {learn-preposition*extract-relations*extract_once*filters
   (state <s> ^name learn-preposition
              ^topstate.filters.filter <fil>
              ^topstate.svs.command <cmd>
              ^topstate.svs.spatial-scene <sc>
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>
           ^obj1 <oa>
           ^obj2 <ob>)
   (<ob> ^id <objb>)
   (<oa> ^id <obja>)   
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>) 
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<cmd> ^extract_once <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <fil>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}
