
sp {find-relations*propose*relation-search*build-answerlist*id
   (state <s> ^name find-relations
              ^prep <type>
              ^prep-link <pl>
              ^obj <id>)
   (<pl> ^prep.name <type>)
-->
   (<s> ^relationships <rels>)
   (<rels> ^with <id>)
}
sp {find-relations*propose*relation-search*build-answerlist*id*prep2
   (state <s> ^name find-relations
              ^prep <type>
              ^prep-link <pl>
              ^obj <id>)
   (<pl> ^prep2.name <type>)
-->
   (<s> ^relationships <rels>)
   (<rels> ^with <id>)
}


sp {find-relations*propose*copy-relationship
   (state <s> ^name find-relations
              ^prep <name>
              ^relationships <rels>)
   (<rels> -^preposition <name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name copy-relationships
        ^rel <rels>)
}
sp {find-relations*apply*copy-relationship*name
   (state <s> ^name find-relations
              ^prep <name>
              ^operator <o>
              ^relationships <rels>)
   (<o> ^name copy-relationships
        ^rel <rels>)
-->
   (<rels> ^preposition <name>)
}

sp {find-relations*apply*copy-relationship
   (state <s> ^name find-relations
              ^prep <name>
             -^prep << linear-with >>
              ^operator <o>
              ^ind <ind>
              ^obj <id>
              ^prep-link.prep <p>
              ^other <ind2>
              ^relationships <rels>)
   (<p> ^name <name> ^pair <pair>)
   (<pair> ^<ind> <id> ^<ind2> <id2>)
   (<o> ^name copy-relationships
        ^rel <rels>)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^obj <id2> ^matched true) 
}
sp {find-relations*apply*copy-relationship*prep2
   (state <s> ^name find-relations
              ^prep <name>
             -^prep << linear-with >>
              ^operator <o>
              ^ind <ind>
              ^obj <id>
              ^prep-link.prep2 <p>
              ^other <ind2>
              ^relationships <rels>)
   (<p> ^name <name> ^pair <pair>)
   (<pair> ^<ind> <id> ^<ind2> <id2>)
   (<o> ^name copy-relationships
        ^rel <rels>)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^obj <id2> ^matched true) 
}
sp {find-relations*apply*copy-relationship*three-args
   (state <s> ^name find-relations
              ^prep {<name> << linear-with >>}
              ^operator <o>
              ^prep-link <pl>
#              ^ind <ind>
              ^obj <id>
#              ^other <ind2>
              ^prep-link.prep2 <p>
              ^relationships <rels>)
   (<p> ^name <name> ^pair <pair>)
   (<pair> ^a <id> ^b <id2> ^c <id3>)
   (<o> ^name copy-relationships
        ^rel <rels>)
-->
   (<rels> ^relations <r>)
   (<r> ^name <name> ^obj1 <id2> ^obj2 <id3> ^matched true) 
}


sp {find-relations*propose*complete-relations-search
   (state <s> ^name find-relations
              ^prep <name>
              ^relationships <rels>)
   (<rels> ^preposition <name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-relations-search
        ^type true
        ^category svs)
}

sp {find-relations*apply*complete-relations-search
   (state <s> ^name find-relations
              ^operator <o>
              ^superstate <ss>
              ^relationships <rels>)
   (<o> ^name complete-relations-search)
-->
   (<ss> ^relationships <rels>)
}
