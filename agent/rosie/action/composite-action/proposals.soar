### propose assign-goal if there is no goal
sp {composite-action*propose*assign-goal
   (state <s> ^problemspace action
   	      ^action-type composite
	      -^desired <any>
	      ^goal-id <gid>
	      ^procedural-id <pid>
	      ^superstate <ss>)
-->
   (<s> ^operator <op> + !)   
   (<op> ^name assign-goal
   	 ^goal-id <gid>
	 ^procedural-id <pid>
	 ^operator-proposal-state <ss>)
}

### propose assign-action-space if there is no action-space
sp {composite-action*propose*assign-action-space
   (state <s> ^problemspace action
   	      ^action-type composite
	      -^action-space-operator <any>
	      ^action-space-id <aid>
	      ^desired <d>
	      ^procedural-id <pid>)
-->
   (<s> ^operator <op> + !)   
   (<op> ^name assign-action-space
   	 ^action-space-id <aid>
	 ^procedural-id <pid>)
}

### if action-space-operators exist, propose them
sp {composite-action*propose*action-space-operators
   (state <s> ^problemspace action
   	      ^action-type composite
	      ^desired <d>
	      ^action-space-operator <op>)
-->
   (<s> ^operator <op> +)   
}


### propose successful completion if the desired state is reached
sp {composite-action*propose*successful
   (state <s> ^problemspace action
   	      ^action-type composite
	      -^problem-space learning
	      ^desired <d>
	      ^success <d>
	      ^superstate.indexed-action <ia>)
-->
   (<ia> ^applied yes)
}