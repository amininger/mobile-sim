#### elaborate state
## elaborate predicate
sp {generate-grounded-relation-predicate*elaborate*predicate
   (state <s> ^name generate-grounded-predicate
   	      ^superstate.operator <sop>
	      ^smem.command <scomm>)
   (<sop> ^predicate-definition <pred>
	  ^superoperator <super>
	  ^desired <d>
	  ^procedural-id <pid>
	  ^type <type>
	  ^action-operator-proposal-state <ss>)
-->
   (<s> ^predicate-definition <pred>
   	^type <type>
	^indexed-action <super>
	^desired <d>
	^grounded-predicate <gp>
	^procedural-id <pid>
	^type <type>
	^action-operator-proposal-state <ss>
	^desired-grounded-predicate <dgp>)
   (<gp> ^_instantiation-of <pred>)
}

### retrieve pred
sp {generate-grounded-relation-predicate*retrieve-predicate
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      -^retrieved-lti <pred>)
#   (<pred> -^<any>)
-->
   (<s> ^nretrieve-lti <pred>)
}

### elaborate arguments that can be filled in from the action-command
sp {generate-grounded-predicate*elaborate*arguments*action-command*level1
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level1*equal
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pobj>)
   (<pred> ^<pred-arg> <nobj>)
   (<nobj> ^equals <pobj>)
   (<ia> ^<parg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level2
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pval>)
   (<pval> ^<pval-arg> <pobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<ia> ^<parg> <ival>)
   (<ival> ^<pval-arg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

sp {generate-grounded-predicate*elaborate*arguments*action-command*level2*equals
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>
	      ^procedural-id <pid>
	      ^indexed-action <ia>)
   (<pid> ^<parg> <pval>)
   (<pval> ^<pval-arg> <nobj>)
   (<pred> ^<pred-arg> <pobj>)
   (<pobj> ^equals <nobj>)
   (<ia> ^<parg> <ival>)
   (<ival> ^<pval-arg> <wme-obj>)
-->
   (<s> ^pairing <pair>)
   (<pair> ^smem-object <pobj>
   	   ^wmem-object <wme-obj>)
}

### elaborate objects that do not have pairings
sp {generate-grounded-predicate*elaborate*arguments*not*in*action-command
   (state <s> ^name generate-grounded-predicate
   	      ^predicate-definition <pred>)
   (<pred> ^{<parg> <> state <> identifier} <pobj>)
  -{(<s> ^pairing <pair>)
    (<pair> ^smem-object <pobj>)}
-->
   (<s> ^not-matched <pobj>)
}

### elaborate retrievals for objects that do-not have pairings
sp {generate-grounded-predicate*retrieve*not-matched
   (state <s> ^name generate-grounded-predicate
   	      ^not-matched <pobj>
	      -^retrieved-lti <pobj>)
#   (<pobj> -^<arg>)
-->
   (<s> ^nretrieve-lti <pobj>)
}

## propose retrieve predicate if substructure is absent
sp {generate-grounded-predicate*retrieve*substructure*relation
   (state <s> ^name generate-grounded-predicate
   	      ^grounded-predicate <gp>
	      -^retrieved-lti <rel>)
   (<gp> ^relation.smem-rep <rel>)
#   (<rel> -^<arg>)
-->
   (<s> ^nretrieve-lti <rel>)
}

############### retrieve preposition ################################
sp {generate-grounded-predicate*retrieve*substructure*relation*substructure
   (state <s> ^name generate-grounded-predicate
   	      ^grounded-predicate <gp>
	      -^retrieved-lti <rel-def>)
   (<gp> ^relation.smem-rep.relation <rel-def>)
#   (<rel-def> -^<sub>)
-->
   (<s> ^nretrieve-lti <rel-def>)
}