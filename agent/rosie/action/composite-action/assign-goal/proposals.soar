## for evey predicate on the goal-id, propose generate-grounded-predicate
sp {assign-goal*propose*generate-grounded-predicate
   (state <s> ^name assign-goal
   	      ^goal-id <gid>
	      ^superoperator <sop>
	      ^bit flip
	      ^desired <d>
	      ^procedural-id <pid>
	      ^action-operator-proposal-state <as>)
   (<gid> ^{<pred-link> << relation-predicate state-predicate >>} <val>)
  -{(<d> ^<pred> <dval>)
    (<dval> ^_instantiation-of <val>)}
  -{(<s> ^failure <f>)
    (<f> ^predicate-definition <val>)}
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name generate-grounded-predicate
	 ^predicate-definition <val>
	 ^superoperator <sop>
	 ^desired <d>
	 ^procedural-id <pid>
	 ^type <pred-link>
	 ^action-operator-proposal-state <as>)
}
## if there are any failures in grounding predicates, propose operator
sp {assign-goal*propose*failure-grounded-predicate
   (state <s> ^name assign-goal
   	      ^failure <f>
	      ^goal-id <gid>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name grounding-predicate-failure
   	 ^goal-id <gid>)
}

sp {assign-goal*propose*failure-grounded-predicate*elaborate
   (state <s> ^name assign-goal
   	      ^failure <f>
	      ^operator <op>)
   (<op> ^name grounding-predicate-failure)
-->
   (<op> ^failure <f>)
}


## complete assign goal
sp {assign-goal*propose*complete-assign-goal
   (state <s> ^name assign-goal
   	      ^desired <d>)
-->
   (<s> ^operator <op> + <)
   (<op> ^name complete-assign-goal
   	 ^desired <d>)
}