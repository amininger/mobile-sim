sp {assign-goal*apply*grounding-predicate-failure
   (state <s> ^name assign-goal
   	      ^operator.name grounding-predicate-failure)
-->
   (<s> ^failure-status <fs>)
   (<fs> ^details <d>)
   (<d> ^unknown attribute)
}

sp {assign-goal*apply*grounding-predicate-failure*details
   (state <s> ^name assign-goal
   	      ^operator <op>
	      ^failure-status.details <d>)
   (<op> ^name grounding-predicate-failure
   	 ^failure <f>)
   (<f> ^details <nd>
   	^predicate-definition <def>)
   (<nd> ^predicate <pred>)
-->
   (<d> ^failed-grounding <fg>)
   (<fg> ^predicate-definition <def>
   	 ^predicate-grounding <pred>)
}

sp {assign-goal*apply*grounding-predicate-failure*topstate
   (state <s> ^name assign-goal
   	      ^operator.name grounding-predicate-failure
	      ^topstate.interaction.status <stat>
	      ^failure-status <fs>)
   (<fs> ^details.failed-grounding <any>)
-->
   (<stat> ^failure <fs>)
   (<fs> ^type failure
   	 ^information unknown-goal)
}

sp {assign-goal*apply*successful*grounding
   (state <s> ^name assign-goal
   	      ^desired <d>
	      ^operator.name grounding-predicate-failure
	      ^failure-status.details <det>)
   (<d> ^achieve-state <pred>)
   (<pred> ^_instantiation-of <ins>)
-->
   (<det> ^successful-grounding <sg>)
   (<sg> ^predicate-definition <ins>
   	 ^predicate-grounding <pred>)
}
