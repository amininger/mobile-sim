sp {topstate*elaborate*action-goal-stack
   (state <s> ^topstate <s>
              ^interaction.stack.segment.goal-stack <stack>)
-->
   (<s> ^action-goal-stack <stack>)
}

sp {topstate*propose*action*achieve*action-goal
   (state <s> ^topstate <s>
              ^action-goal-stack <stack>)
   (<stack> ^action-goal)
-->
   (<s> ^operator <o> + >)
   (<o> ^name action
        ^action-goal-stack <stack>)
}

sp {action*elaborate*action-goal-stack
   (state <s> ^name action
              ^superstate.action-goal-stack <stack>)
-->
   (<s> ^action-goal-stack <stack>)
}

sp {action*propose*achieve*goal
   (state <s> ^name action
              ^action-goal-stack.action-goal <goal>)
   (<goal> ^name <goal-name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name <goal-name>
        ^action-goal <goal>)
}

sp {action*apply*achieve*goal*achieved
   (state <s> ^name action
              ^operator <o>
              ^action-goal-stack <stack>)
   (<o> ^name <goal-name>
        ^action-goal <goal>)
   (<stack> ^action-goal <goal>)
   (<goal> ^name <goal-name>
           ^achieved true)
-->
   (<stack> ^action-goal <goal> -)
}

sp {action*problemspace*elaborate*action-goal
   (state <s> ^superstate.operator.action-goal <goal>)
-->
   (<s> ^action-goal <goal>)
}

sp {action*elaborate*global-parameters*stack
   (state <s> ^name action
              ^action-goal-stack <stack>)
   (<stack> ^global-parameters <gp>
            ^action-goal <goal>)
-->
   (<goal> ^global-parameters <gp>)
}

sp {action*problemspace*elaborate*action-goal*global-parameters
   (state <s> ^action-goal <goal>
              ^superstate.action-goal.global-parameters <gp>)
-->
   (<goal> ^global-parameters <gp>)
}


sp {action*problemspace*elaborate*move-metadata
   (state <s> ^action-goal <goal>)
   (<goal> ^action-goal <subgoal>
           ^move-metadata <meta>)
-->
   (<subgoal> ^move-metadata <meta>)
}

sp {action*problemspace*propose*achieve*subgoal
   (state <s> ^action-goal.action-goal <subgoal>)
   (<subgoal> ^name <subgoal-name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name <subgoal-name>
        ^action-goal <subgoal>)
}

sp {action*problemspace*elaborate*achieve*subgoal*parameters
   (state <s> ^problemspace action
              ^operator <o> +)
   (<o> ^name <subgoal-name>
        ^action-goal <subgoal>)
   (<subgoal> ^parameters.<param> <val>)
-->
   (<o> ^<param> <val>)
}

sp {action*problemspace*apply*achieve*subgoal*remove
   (state <s> ^operator <o>
              ^action-goal <goal>)
   (<o> ^name <subgoal-name>
        ^action-goal <subgoal>)
   (<subgoal> ^name <subgoal-name>
              ^achieved true)
   (<goal> ^action-goal <subgoal>)
-->
   (<goal> ^action-goal <subgoal> -
           ^achieved-subgoal <subgoal>)
}