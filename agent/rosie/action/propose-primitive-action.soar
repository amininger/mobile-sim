####### propose pick-up for objects #########################################
sp {propose*pickup*object
   (state <s> ^problemspace action
             -^problemspace perception # AM: Hack for now so they don't get proposed while attending to the scene
   	      -^action-type primitive
   	      ^object <obj>
	      -^grabbed <any>)
   (<obj> -^category location)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_pick-up-object
   	 ^argument1 <obj>
	 ^action-type primitive)
}

######## propose put an object in a position #################################
sp {propose*put-down
   (state <s> ^problemspace action
             -^problemspace perception # AM: Hack for now so they don't get proposed while attending to the scene
   	      -^action-type primitive
   	      ^grabbed <gobj>
   	      ^object {<obj> <> <gobj>})
-->
   (<s> ^operator <op> +)
   (<op> ^name op_put-down-object-location
   	 ^argument1 <gobj>
	 ^argument2 <new>
	 ^action-type primitive)
   (<new> ^argument <obj>)
}

###################### propose open location #################################
sp {propose*open*location
   (state <s> ^problemspace action
             -^problemspace perception # AM: Hack for now so they don't get proposed while attending to the scene
   	      -^action-type primitive
   	      ^object <obj>
	      ^closed <obj>)
 -->
   (<s> ^operator <op> +)
   (<op> ^name op_open-location
   	 ^argument1 <obj>
	 ^action-type primitive)
}

###################### propose close location ##################################
sp {propose*close*location
   (state <s> ^problemspace action
             -^problemspace perception # AM: Hack for now so they don't get proposed while attending to the scene
   	      -^action-type primitive
   	      ^object <obj>
	      ^open <obj>)
 -->
   (<s> ^operator <op> +)
   (<op> ^name op_close-location
   	 ^argument1 <obj>
	 ^action-type primitive)
}

######################### propose wait ########################################
sp {propose*op_wait*operator
   (state <s> ^problemspace action
   	      -^action-type primitive
	      -^name selection)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_wait
   	 ^action-type primitive)
}
   

#################### point #######################################	    	     
 
sp {propose*point*object
   (state <s> ^problemspace action
             -^problemspace perception # AM: Hack for now so they don't get proposed while attending to the scene
   	      -^action-type primitive
   	      ^object <obj>)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_point-to-object
   	 ^argument1 <nobj>
	 ^action-type primitive)
   (<nobj> ^argument <obj>)
}
   

#################### activate #################################
sp {propose*activate*stove
   (state <s> ^problemspace action
   	      -^problemspace perception
	      -^action-type primitive
	      ^object <obj>
	      ^off <obj>)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_activate
   	 ^argument1 <obj>
	 ^action-type primitive)
}

#################### stop #################################
sp {propose*stop*stove
   (state <s> ^problemspace action
   	      -^problemspace perception
	      -^action-type primitive
	      ^object <obj>
	      ^on <obj>)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_stop
   	 ^argument1 <obj>
	 ^action-type primitive)
}