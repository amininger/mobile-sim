## elaborate indexed-action from operator
sp {action*elaborate*indexed-action*operator
   (state <s> ^name action
   	      ^superstate.operator.indexed-action <ia>)
-->
   (<s> ^indexed-action <ia>
   	^action-matches <am>)
}

## elaborate indexed-action from purpose
sp {action*elaborate*indexed-action
   (state <s> ^name action
   	      ^superstate.operator.purpose.parameters.indexable.result <iop>)
-->
   (<s> ^indexed-action <iop>
   	^action-matches <am>)
}


sp {action*elaborate*action-matches*operators
   (state <s> ^name action
   	      ^operator <op> +
	      ^action-matches <am>)
-->
   (<am> ^match-set <ms>)
   (<ms> ^operator <op>)
}

## elaborate lexical-information
sp {action*elaborate*lexical-information
   (state <s> ^name action
   	      ^superstate.operator.purpose.parameters.indexable.lexical <iop>)
-->
   (<s> ^lexical-information <iop>)
}


## elaborate problemspace
sp {action*elaborate*problem-space
   (state <s> ^name action)
-->
   (<s> ^problemspace action
   	^monitor-scene-changes true)
}

## copy action problemspace
sp {action*copy*problem-space
   (state <s> ^superstate <ss>
   	      -^name assign-goal
	      -^name multiple-argument-instantiations)
   (<ss> ^problemspace action)
-->
   (<s> ^problemspace action)
}

## elaborate action-type
sp {composite-action*elaborate*composite
   (state <s> ^superstate.operator.action-type <any>)
-->
   (<s> ^action-type <any>)
}

sp {composite-action*copy*action-type
   (state <s> ^superstate.action-type <any>)
-->
   (<s> ^action-type <any>)
}


### 
sp {action*elaborate*ignore-set
   (state <s> ^problemspace action)
-->
   (<s> ^ignore-set <nm>)
   (<nm> ^attribute applied
   	 ^attribute information
	 ^attribute _relation
	 ^attribute _metadata
	 ^attribute actions
	 ^attribute action-type
	 ^attribute matched
	 ^attribute _instantiation-of)
}


### if an action-operator is selected, mark purpose
sp {action*operator-selection*mark-purpose
   (state <s> ^problemspace action
   	      ^operator <op>
	      ^superstate.operator.purpose <p>)
   (<op> ^action-type <type>)
-->
   (<p> ^executing action)
}


### if an action is selected, delete all other object argumentation on indexed operator 

sp {action*operator-selection*delete-argument*direct-object
   (state <s> ^problemspace action
   	      ^operator <op>
	      ^indexed-action <ia>)
   (<op> ^<arg> <val>
   	 ^action-type primitive)
   (<val> ^id)
   (<ia> ^<arg> {<nval> <> <val>})
-->
   (<ia> ^<arg> <nval> -)
}

sp {action*operator-selection*delete-argument*pp-object
   (state <s> ^problemspace action
   	      ^operator <op>
	      ^indexed-action <ia>)
   (<op> ^<arg> <val>
   	 ^action-type primitive)
   (<val> ^argument <obj>)
   (<ia> ^<arg> <nval>)
   (<nval> ^argument {<nobj> <> <obj>})
-->
   (<ia> ^<arg> <nval> -)
}
