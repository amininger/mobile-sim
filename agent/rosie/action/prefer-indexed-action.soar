sp {match*name*operators
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>
	      ^action-matches.match-set <ms>)
   (<ms> ^operator <op>)
   (<ia> ^name <ia-name>)
   (<op> ^name <ia-name>)
-->
   (<ms> ^matched name)
}

sp {mark*operators*match*argument*level1
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>
	      ^action-matches.match-set <ms>)
   (<ms> ^operator <op>)
   (<ia> ^{<arg> <> name} <val>)
   (<op> ^<arg> <val>)
-->
   (<ms> ^matched <arg>)
}

sp {mark*operators*match*argument*level2*no-relation
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>
 	      ^action-matches.match-set <ms>)
   (<ms> ^operator <op>)
   (<ia> ^<arg> <val>)
   (<val> ^argument <obj>
   	  -^relation)
   (<op> ^<arg> <nval>)
   (<nval> ^argument <obj>)
-->
   (<ms> ^matched <arg>)
}

sp {mark*operators*match*argument*level2*with-relation
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>
 	      ^action-matches.match-set <ms>)
   (<ms> ^operator <op>)
   (<ia> ^<arg> <val>)
   (<val> ^argument <obj>
   	  ^relation <rel>)
   (<op> ^<arg> <nval>)
   (<nval> ^argument <obj>
   	   ^relation <rel>)
-->
   (<ms> ^matched <arg>)
}

sp {elaborate*operator*match
   (state <s> ^name action
   	      ^operator <op> + 
   	      ^indexed-action <map-op> <> <op>
	      -^ignore-set.attribute <attr>
	       ^action-matches.match-set <ms>)
   (<ms> ^operator <op>)
   (<map-op> ^<attr> <val>)
   (<ms> -^matched <attr>)
   # AM: added so op_find-object isn't rejected
   (<op> -^name op_find-object)
-->
   (<op> ^op-matched no)
   (<s> ^operator <op> -)
}


### augment spatial-relationship to operator
sp {augment*spatial-relationship
   (state <s> ^name action
   	      ^indexed-action <iop>
   	      ^operator <op> +)
   (<iop> ^<arg> <val>)
   (<val> ^relation <rel>
   	  ^<arg2> <obj>)
   (<op> ^<arg> <nval>)
   (<nval> ^<arg2> <obj>)
-->
   (<nval> ^relation <rel>)
}


############ special handling for the operator op_wait 
sp {mark*operators*match*op_wait
   (state <s> ^name action
   	      ^operator <op> +
	      ^indexed-action <ia>
 	      ^action-matches.match-set <ms>)
   (<ms> ^operator <op>)
   (<ia> ^{<arg> <> name} <val>)
   (<op> ^name op_wait)
-->
   (<ms> ^matched <arg>)
}