sp {resolve-instantiation*elaborate-state
   (state <s> ^name resolve-instantiation
   	      ^superstate.operator <op>)
   (<op> ^argument <arg>
   	 ^indexed-action <ia>
	 ^operator-proposal-state <ss>)
-->
   (<s> ^argument <arg>
   	^indexed-action <ia>
	^operator-proposal-state <ss>)
}

sp {resolve-instantiation*elaborate-object-link*direct-object
   (state <s> ^name resolve-instantiation
   	      ^argument <arg>
	      ^indexed-action._metadata.map-id <met>
	      ^operator-proposal-state.superstate.operator.purpose.parameters.indexable <param>)
   (<param> ^direct-object <do>)
   (<met> ^procedural.<arg> <objid>
   	  ^linguistic.direct-object <objid>)
-->
   (<s> ^object-link <do>)
}

sp {resolve-instantiation*elaborate-object-link*pp-object
   (state <s> ^name resolve-instantiation
   	      ^argument <arg>
	      ^indexed-action <ia> 
	      ^operator-proposal-state.superstate.operator.purpose.parameters.indexable <param>)
   (<param> ^pp-object.object <do>)
   (<met> ^procedural.<arg>.argument <objid>
   	  ^linguistic.pp-object.object <objid>)
   (<ia> ^_metadata.map-id <met>
   	 ^<arg> <val>)
-->
   (<s> ^object-link <do>)
}

sp {resolve-instantiation*best-resolution*direct-object
   (state <s> ^name resolve-instantiation
   	      ^object-link <link>
	      ^superstate <ss>
	      ^argument <arg>
	      ^winner <val>
	      ^indexed-action <ia>)
   (<ia> ^<arg> <val>)
-->
   (<ss> ^resolution-result <res>)
   (<res> ^resolution-type BEST
   	  ^argument <arg>
	  ^value <val>
	  ^object-link <link>)
}

sp {resolve-instantiation*best-resolution*pp-object
   (state <s> ^name resolve-instantiation
   	      ^object-link <link>
	      ^superstate <ss>
	      ^argument <arg>
	      ^winner <obj>
	      ^indexed-action <ia>)
   (<ia> ^<arg> <val>)
   (<val> ^argument <obj>)
-->
   (<ss> ^resolution-result <res>)
   (<res> ^resolution-type BEST
   	  ^argument <arg>
	  ^value <val>
          ^object-link <link>)
}

sp {resolve-instantiation*best-resolution*delete-objects*not-winner
   (state <s> ^name resolve-instantiation
   	      ^object-link <link>
	      ^winner <obj>)
   (<link> ^result {<res> <> <obj>})
-->
   (<link> ^result <res> -)}
