sp {measure-object*elaborate*object-to-measure
   (state <s> ^name measure-object
              ^superstate.operator.object <obj>)
-->
   (<s> ^object-to-measure <obj>)
}

sp {measure-object*elaborate*train-sense
   (state <s> ^name measure-object
              ^superstate.operator.train-sense <sense>)
-->
   (<s> ^train-sense <sense>)
}

sp {measure-object*elaborate*property-to-measure
   (state <s> ^name measure-object
              ^superstate.operator.property <prop>)
-->
   (<s> ^property-to-measure <prop>)
}

sp {measure-object*elaborate*property-to-measure*from*sense
   (state <s> ^name measure-object
              ^train-sense.property <prop>)
-->
   (<s> ^property-to-measure <prop>)
}

sp {measure-object*elaborate*location-to-measure-at
   (state <s> ^name measure-object
              ^topstate.object <loc>
              ^property-to-measure.location <loc-name>)
   (<loc> ^property <name-prop>)
   (<name-prop> ^name name
                ^word <loc-name>)
-->
   (<s> ^location-to-measure-at <loc>)
}

sp {measure-object*elaborate*object-on-location*true
   (state <s> ^name measure-object
              ^object-to-measure.id <id>
              ^location-to-measure-at.id <loc>
              ^topstate.prepositions.prep <prep>)
   (<prep> ^name on
           ^pair <p>)
   (<p> ^a <id>
        ^b <loc>)
-->
   (<s> ^object-on-location true)
}

sp {measure-object*elaborate*object-on-location*false
   (state <s> ^name measure-object
              -^object-on-location true)
-->
   (<s> ^object-on-location false)
}

sp {measure-object*elaborate*measure-after-time
   (state <s> ^name measure-object
              ^action-goal.measure-after-time <t>)
-->
   (<s> ^measure-after-time <t>)
}