sp {measure-object*propose*create-make-measurement-subgoal
   (state <s> ^name measure-object
              ^object-on-location true
             -^action-goal.action-goal.name make-measurement
             -^action-goal.achieved-subgoal.name make-measurement
              ^object-to-measure <obj>
              ^property-to-measure.name <prop-name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name create-make-measurement-subgoal
        ^parameters <p>)
   (<p> ^object <obj>
        ^property-name <prop-name>)
}

sp {measure-object*apply*create-make-measurement-subgoal
   (state <s> ^name measure-object
              ^operator <o>
              ^action-goal <goal>)
   (<o> ^name create-make-measurement-subgoal
        ^parameters <p>)
-->
   (<goal> ^action-goal <subgoal>)
   (<subgoal> ^name make-measurement
              ^parameters <p>)
}

sp {measure-object*make-measurement*elaborate*parameters
   (state <s> ^name make-measurement
              ^superstate.operator <o>)
   (<o> ^object <obj>
        ^property-name <prop-name>)
-->
   (<s> ^object-to-update <obj>
        ^property-name <prop-name>)
}

sp {measure-object*make-measurement*elaborate*attention-info
   (state <s> ^name make-measurement
              ^object-to-update.id <id>)
-->
   (<s> ^attend-to-all-scene-changes true
        ^attend-to-object-id <id>)
}

sp {measure-object*make-measurement*remove*action*problemspace
   (state <s> ^name make-measurement)
-->
   (<s> ^problemspace action -)
}

sp {measure-object*make-measurement*propose*wait
   (state <s> ^name make-measurement)
-->
   (<s> ^operator <o> + <)
   (<o> ^name wait)
}

sp {measure-object*make-measurement*apply*wait*mark*achieved
   (state <s> ^name make-measurement
              ^operator.name wait
              ^action-goal <goal>)
-->
   (<goal> ^achieved true)
}

sp {measure-object*make-measurement*mark*achieved
   (state <s> ^name attend-to-scene-change
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name attend-to-different-feature-val
        ^object.monitor-id <id>
        ^property.name <prop-name>)
   (<ss> ^name make-measurement
         ^action-goal <goal>
         ^property-name <prop-name>
         ^object-to-update.id <id>)
-->
   (<goal> ^achieved true)
}


