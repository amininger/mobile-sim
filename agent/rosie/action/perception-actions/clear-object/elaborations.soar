sp {clear-object*elaborate*object-to-clear
   (state <s> ^name clear-object
              ^superstate.operator.object <obj>)
-->
   (<s> ^object-to-clear <obj>)
}

sp {clear-object*elaborate*current-bin
   (state <s> ^name clear-object
              ^object-to-clear.id <id>
              ^topstate.object <bin>
              ^topstate.prepositions.prep <on>)
   (<bin> ^id <bin-id>
          ^is-bin true)
   (<on> ^name on
         ^pair <p>)
   (<p>  ^a <id>
         ^b <bin-id>)
-->
   (<s> ^current-bin <bin>)
}

sp {clear-object*elaborate*blocking-object
   (state <s> ^name clear-object
              ^object-to-clear.id <id>
              ^topstate.object <blocker>
              ^topstate.prepositions.prep <on>)
   (<blocker> ^id <blocker-id>)
   (<on> ^name on
         ^pair <p>)
   (<p>  ^a <blocker-id>
         ^b <id>)
-->
   (<s> ^blocking-object <blocker>)
}
   