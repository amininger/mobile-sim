sp {clear-object*propose*create-stash-object-subgoal
   (state <s> ^name clear-object
              ^blocking-object <blocker>
              ^current-bin <bin>
             -^action-goal.action-goal.name stash-object
             -^action-goal.achieved-subgoal.parameters.object <blocker>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name create-stash-object-subgoal
        ^parameters <p>)
   (<p> ^object <blocker>
        ^exclude-bin <bin>)
}

sp {clear-object*prefer*create-stash-object-subgoal*on*top
   (state <s> ^name clear-object
              ^operator <o1> +
              ^operator <o2> +
              ^topstate.prepositions.prep <on>)
   (<o1> ^name create-stash-object-subgoal
         ^parameters.object.id <id1>)
   (<o2> ^name create-stash-object-subgoal
         ^parameters.object.id <id2>)
   (<on> ^name on
         ^pair <p>)
   (<p> ^a <id1>
        ^b <id2>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {clear-object*apply*create-stash-object-subgoal
   (state <s> ^name clear-object
              ^operator <o>
              ^action-goal <goal>)
   (<o> ^name create-stash-object-subgoal
        ^parameters <p>)
-->
   (<goal> ^action-goal <subgoal>)
   (<subgoal> ^name stash-object
              ^parameters <p>)
}

sp {clear-object*propose*stash*blocker
   (state <s> ^name clear-object
              ^stash-object <blocker>
              ^current-bin <bin>
             -^stashed-object <blocker>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name stash-object
        ^object <blocker>
        ^exclude-bin <bin>)
}