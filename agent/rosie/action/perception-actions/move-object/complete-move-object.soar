sp {move-object*propose*complete-move-object
   (state <s> ^name move-object
              ^performed-move true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-move-object)
}

sp {move-object*apply*complete-move-object*mark*achived
   (state <s> ^name move-object
              ^operator.name complete-move-object
              ^action-goal <goal>)
-->
   (<goal> ^achieved true)
}

sp {move-object*apply*complete-move-object*remove*grabbed
   (state <s> ^name move-object
              ^operator.name complete-move-object
              ^io.input-link.self <self>
              ^topstate <top>)
   (<self> ^grabbed-object -1)
   (<top> ^grabbed <obj>)
-->
   (<top> ^grabbed <obj> -)
}

sp {move-object*apply*complete-move-object*cleanup*focus
   (state <s> ^name move-object
              ^operator.name complete-move-object
              ^{ << object-to-move destination >> } <obj>)
   (<obj> ^focus <any>)
-->
   (<obj> ^focus <any> -)
}
   
sp {move-object*apply*complete-move-object*cleanup*nfocus
   (state <s> ^name move-object
              ^operator.name complete-move-object
              ^{ << object-to-move destination >> } <obj>)
   (<obj> ^nfocus <any>)
-->
   (<obj> ^nfocus <any> -)
}
   