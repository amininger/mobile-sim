sp {find-object*propose*lookup-word
   (state <s> ^name find-object
              ^object-desc.word <word>
             -^action-goal.indexed-word.result.word <word>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name index-parameter
        ^word <word>)
}

sp {find-object*elaborate*index-parameter*parameter
   (state <s> ^name find-object
              ^operator <o> +
              ^action-goal.indexed-word <param>)
   (<o> ^name index-parameter
        ^word <word>)
   (<param> ^lexical <word>)
-->
   (<o> ^parameter <param>)
}

sp {find-object*apply*index-parameter*create*parameter
   (state <s> ^name find-object
              ^operator <o>
              ^action-goal <goal>)
   (<o> ^name index-parameter
        ^word <word>)
-->
   (<goal> ^indexed-word <i>)
   (<i> ^type word
        ^lexical <word>
        ^index-info.use-interaction true)
}

sp {find-object*index-parameter*remove*action*problemspace
   (state <s> ^name index-parameter
              ^superstate.name find-object)
-->
   (<s> ^problemspace action -)
}