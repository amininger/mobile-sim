sp {stash-object*propose*create-move-object-subgoal
   (state <s> ^name stash-object
              ^object-to-stash <obj>
              ^bin-counts.bin-count <bc>
             -^action-goal.action-goal.name move-object
             -^action-goal.achieved-subgoal.name move-object)
   (<bc> ^count <c>
         ^top-object <dest>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name create-move-object-subgoal
        ^count <c>
        ^parameters <p>)
   (<p> ^object <obj>
        ^destination <dest>)
}

sp {stash-object*prefer*lower*bin-counts
   (state <s> ^name stash-object
              ^operator <o1> +
              ^operator <o2> +
              ^bin-counts <bcs>)
   (<o1> ^name create-move-object-subgoal
         ^count <c1>)
   (<o2> ^name create-move-object-subgoal
         ^count > <c1>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {stash-object*apply*create-move-object-subgoal
   (state <s> ^name stash-object
              ^operator <o>
              ^action-goal <goal>)
   (<o> ^name create-move-object-subgoal
        ^parameters <p>)
-->
   (<goal> ^action-goal <subgoal>)
   (<subgoal> ^name move-object
              ^parameters <p>)
}
