sp {send-message*propose*create-teaching-request
   (state <s> ^name send-message
              ^message.type teaching-request
             -^created-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-teaching-request)
}

sp {send-message*apply*create-teaching-request
   (state <s> ^name send-message
              ^operator.name create-teaching-request
              ^message.target-object <obj>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type teaching-request
          ^originator agent
          ^fields.description <desc>)
   (<s> ^object-to-describe <otd>)
   (<otd> ^source <obj>
          ^field <desc>)
}

sp {send-message*apply*create-teaching-request*preposition
   (state <s> ^name send-message
              ^operator.name create-teaching-request
              ^message.target-preposition <prep>)
-->
   (<s> ^created-message <msg>)
   (<msg> ^type teaching-request
          ^originator agent
          ^fields <f>)
   (<f> ^description <desc>
        ^preposition <prep>)
}