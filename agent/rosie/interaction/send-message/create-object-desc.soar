sp {send-message*propose*create-object-desc
   (state <s> ^name send-message
              ^object-to-describe <otd>)
   (<otd> ^source <obj>
         -^finished)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
}

sp {send-message*apply*create-object-desc*mark*finished
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
-->
   (<otd> ^finished true)
}

sp {send-message*apply*create-object-desc*copy*adjective*non-shape
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
   (<otd> ^source <obj>
          ^field <desc>)
   (<obj> ^lexical.word <word>
          ^indexable <i>)
   (<i> ^type word
        ^lexical <word>
        ^result.sense.property <prop>)
   (<prop> ^type visual
           ^name <> shape)
-->
   (<desc> ^adjective <word>)
}

sp {send-message*apply*create-object-desc*copy*adjective*shape
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
   (<otd> ^source <obj>
          ^field <desc>)
   (<obj> ^lexical.word <word>
          ^indexable <i>)
   (<i> ^type word
        ^lexical <word>
        ^result.sense.property.name name)
-->
   (<desc> ^adjective <word>)
}

sp {send-message*apply*create-object-desc*copy*word*shape
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
   (<otd> ^source <obj>
          ^field <desc>)
   (<obj> ^lexical.word <word>
          ^indexable <i>
         -^indexable.result.sense.property.name name)
   (<i> ^type word
        ^lexical <word>
        ^result.sense.property.name shape)
-->
   (<desc> ^word <word>)
}

sp {send-message*apply*create-object-desc*copy*word*name
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
   (<otd> ^source <obj>
          ^field <desc>)
   (<obj> ^lexical.word <word>
          ^indexable <i>)
   (<i> ^type word
        ^lexical <word>
        ^result.sense.property.name name)
-->
   (<desc> ^word <word>)
}

sp {send-message*apply*create-object-desc*copy*word*object
   (state <s> ^name send-message
              ^operator <o>)
   (<o> ^name create-object-desc
        ^object-to-describe <otd>)
   (<otd> ^source <obj>
          ^field <desc>)
   (<obj> ^lexical.word <word>
         -^indexable.result.sense.property.name name
         -^indexable.result.sense.property.name shape)
-->
   (<desc> ^word object)
}
