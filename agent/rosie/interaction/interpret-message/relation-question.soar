sp {interpret-message*propose*relation-question
   (state <s> ^name interpret-message
              ^instructor-message <msg>)
   (<msg> ^type relation-question
         -^processed)
-->
   (<s> ^operator <o> +)
   (<o> ^name relation-question
        ^category communication
        ^message <msg>)
}

# Elaborate useful information from the message to the operator
sp {interpret-message*elaborate*relation-question*object
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name relation-question
        ^message <msg>)
   (<msg> ^information.object <obj>)
-->
   (<o> ^object <obj>)
}

sp {elaborate*relation-question*relation
   (state <s> ^operator <o> +)
   (<o> ^name relation-question
        ^message <msg>)
   (<msg> ^information.relation <rel>)
-->
   (<o> ^relation <rel>)
}

sp {interpret-message*elaborate*relation-question*specifier
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name relation-question
        ^message <msg>)
   (<msg> ^information.object.specifier <spec>)
-->
   (<o> ^specifier <spec>)
}

sp {interpret-message*elaborate*relation-question*question-word
   (state <s> ^name interpret-message
              ^operator <o> +)
   (<o> ^name relation-question
        ^message <msg>)
   (<msg> ^information.question-word <word>)
-->
   (<o> ^question-word <word>)
}


# Mark Processed
sp {interpret-message*apply*relation-question*mark*processed
   (state <s> ^operator <o>)
   (<o> ^name relation-question
        ^message <msg>)
-->
   (<msg> ^processed true)
}

################################################################
# 
# what-question
#   E.g. 'What is left of the red triangle?' 
# 
################################################################
# Handles what-questions

sp {interpret-message*apply*relation-question*what-question
   (state <s> ^name interpret-message
              ^operator <o>)
   (<o> ^name relation-question
        ^question-word what
        ^message <msg>
        ^relation <rel>)
   (<rel> ^p2.object <objb> ^word <prep>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator mentor)
   (<p> ^type describe-relation
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^indexable <i>
             ^indexable <i2>)
   (<i> ^type object
        ^lexical <objb>
        ^index-info <idx-info>)
   (<i2> ^type preposition
        ^lexical <prep>
        ^index-info <idx-info2>)
   (<idx-info> ^use-interaction true
               ^return-all false
               ^infer-match true)
   (<idx-info2> ^use-interaction true
               ^return-all false
               ^infer-match false)
   (<sat> ^dialog-event.type describe)
}

################################################################
# 
# is-question
#   E.g. 'Is the red triangle in the pantry?' 
# 
################################################################
# Handles is-questions todo
