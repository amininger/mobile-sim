sp {interpret-message*elaborate*il-message
   (state <s> ^name interpret-message
              ^superstate.operator.message <msg>)
-->
   (<s> ^il-message <msg>)
}

sp {interpret-message*elaborate*instructor-message
   (state <s> ^name interpret-message
              ^il-message.id <id>)
-->
   (<s> ^instructor-message <i-msg>)
   (<i-msg> ^id <id>
            ^speaker instructor)
}

sp {interpret-message*elaborate*instructor-message*fields
   (state <s> ^name interpret-message
              ^il-message.fields <f>
              ^instructor-message <msg>)
-->
   (<msg> ^information <f>)
}

sp {interpret-message*elaborate*instructor-message*information
   (state <s> ^name interpret-message
              ^il-message.information <inf>
              ^instructor-message <msg>)
-->
   (<msg> ^information <inf>)
}

sp {interpret-message*elaborate*instructor-message*type
   (state <s> ^name interpret-message
              ^il-message.type <type>
              ^instructor-message <msg>)
-->
   (<msg> ^type <type>)
}

sp {interpret-message*elaborate*instructor-message*no-fields
   (state <s> ^name interpret-message
              ^il-message <msg>
              ^instructor-message <i-msg>)
   (<msg> ^id <id>
          ^<dialog-type> {<info> <> <id>}
         -^fields
         -^type)
-->
   (<i-msg> ^type <dialog-type>
            ^information <info>)
}


sp {interpret-message*elaborate*message-context
   (state <s> ^name interpret-message
              ^topstate.interaction.stack.segment.context <context>)
-->
   (<s> ^message-context <context>)
}

sp {interpret-message*elaborate*current-purpose
   (state <s> ^name interpret-message
              ^topstate.interaction.stack.segment.purpose <p>)
-->
   (<s> ^current-purpose <p>)
}

