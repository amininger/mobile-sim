# For an index-word failure, where the word hasn't been seen before
# This will cause the agent to ask for the kind of property the word is for

sp {interpret-status*propose*interpret-index-word-failure
   (state <s> ^name interpret-status
              ^status <status>
             -^handled <status>)
   (<status> ^type index-word-failure)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-index-word-failure)
}

sp {interpret-status*apply*interpret-index-word-failure*mark*handled
   (state <s> ^name interpret-status
              ^operator.name interpret-index-word-failure
              ^status <status>)
-->
   (<s> ^handled <status>)
}

sp {interpret-status*apply*interpret-index-word-failure*create*new-segment
   (state <s> ^name interpret-status
              ^operator.name interpret-index-word-failure
              ^status.word <word>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type ask-property-name
        ^word <word>
        ^satisfaction.dialog-event <de>)
   (<de> ^type object-message
         ^originator instructor)
}

sp {interpret-status*apply*interpret-index-word-failure*create*message
   (state <s> ^name interpret-status
              ^operator.name interpret-index-word-failure
              ^status.word <word>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type ask-property-name
          ^word <word>)
}




   