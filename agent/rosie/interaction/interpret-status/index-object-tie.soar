# For an index-object-tie, where multiple objects match..
# Will ask a which question to get more information

sp {interpret-status*propose*interpret-index-object-tie
   (state <s> ^name interpret-status
              ^status <status>
             -^handled <status>)
   (<status> ^type index-object-tie
             ^target-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-index-object-tie)
}

sp {interpret-status*apply*interpret-index-object-tie*mark*handled
   (state <s> ^name interpret-status
              ^operator.name interpret-index-object-tie
              ^status <status>)
-->
   (<s> ^handled <status>)
}

sp {interpret-status*apply*interpret-index-object-tie*create*new-segment
   (state <s> ^name interpret-status
              ^operator.name interpret-index-object-tie
              ^status.target-object <obj>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
        ^originator agent)
   (<p> ^type which-question
        ^target-object <obj>
        ^satisfaction.dialog-event <de>)
   (<de> ^type object-message
         ^originator instructor)
}

sp {interpret-status*apply*interpret-index-object-tie*create*message
   (state <s> ^name interpret-status
              ^operator.name interpret-index-object-tie
              ^status.target-object <obj>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type which-question
          ^target-object <obj>)
}

