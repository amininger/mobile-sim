# For an index-word failure, where the word hasn't been seen before
# This will cause the agent to ask for the kind of property the word is for

sp {interpret-status*propose*interpret-index-preposition-failure
   (state <s> ^name interpret-status
              ^status <status>
             -^handled <status>)
   (<status> ^type index-preposition-failure)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-index-preposition-failure)
}

sp {interpret-status*apply*interpret-index-preposition-failure*mark*handled
   (state <s> ^name interpret-status
              ^operator.name interpret-index-preposition-failure
              ^status <status>)
-->
   (<s> ^handled <status>)
}

sp {interpret-status*apply*interpret-index-preposition-failure*create*new-segment
   (state <s> ^name interpret-status
              ^operator.name interpret-index-preposition-failure
              ^status.preposition <prep>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
        ^originator agent)
   (<p> ^type teaching-request
        ^target-object <prep>
        ^satisfaction.dialog-event <de>)
   (<de> ^type single-word-response
         ^originator instructor)
}

sp {interpret-status*apply*interpret-index-preposition-failure*create*message
   (state <s> ^name interpret-status
              ^operator.name interpret-index-preposition-failure
              ^status.preposition <prep>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type teaching-request
          ^target-preposition <prep>)
}




   