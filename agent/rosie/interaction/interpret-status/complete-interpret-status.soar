sp {interpret-status*propose*complete-interpret-status
   (state <s> ^name interpret-status)
-->
   (<s> ^operator <o> + <)
   (<o> ^name complete-interpret-status)
}

# AM: IFIX: Disabled cause the substate will go away anyway
#sp {interpret-status*apply*complete-interpret-status
#   (state <s> ^name interpret-status
#              ^operator.name complete-interpret-status
#              ^status <status>
#              ^superstate <ss>)
#-->
#   (<ss> ^interpreted <status>)
#}

sp {interpret-status*apply*complete-interpret-status*clear*status
   (state <s> ^name interpret-status
              ^operator.name complete-interpret-status
              ^topstate.interaction <int>)
   (<int> ^status <status>)
-->
   (<int> ^status <status> -
   	^status <new-status>)
}

sp {interpret-status*apply*complete-interpret-status*copy*new-segment-info
   (state <s> ^name interpret-status
   	      ^operator.name complete-interpret-status
	      ^new-segment-info <info>
	      ^topstate.interaction.changes <c>)
-->
   (<c> ^new-segment-info <info>)
}

sp {interpret-status*apply*complete-interpret-status*copy*outgoing-message
   (state <s> ^name interpret-status
   	      ^operator.name complete-interpret-status
	      ^outgoing-message <msg>
	      ^topstate.interaction.changes <c>)
-->
   (<c> ^outgoing-message <msg>)
}

### if the status matches the purpose of the current segment, mark the current segment to be deleted.
sp {interpret-status*satisfied*purpose
   (state <s> ^name interpret-status
   	      ^topstate.interaction <i>
	      ^operator.name complete-interpret-status)
   (<i> ^stack.segment <seg>
        ^changes <c>
        ^status <stat>)
   (<stat> ^performed <performed>)
   (<seg> ^purpose.satisfaction <sat>)
   (<sat> ^<event> <sat-event>)
   (<sat-event> ^type <event-type>)
   (<performed> ^<event> <status-event>)
   (<status-event> ^type <event-type>)
   # Everything listed in the satisfaction is on the status
   -{(<sat-event> ^{<att> <> info} <val>)
     (<status-event> -^<att> <val>)}
-->
   (<c> ^terminate-segment <seg>)
   (<seg> ^was-satisfied true)
}
