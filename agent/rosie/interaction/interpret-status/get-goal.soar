sp {interpret-status*propose*unknown-goal
   (state <s> ^name interpret-status
   	      ^status <stat>
	      -^new-segment-info)
   (<stat> ^type failure
   	   ^information unknown-goal
	   -^details)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-goal)
}

sp {interpret-status*propose*unknown-goal*details
   (state <s> ^name interpret-status
   	      ^status <stat>
	      -^new-segment-info)
   (<stat> ^type failure
   	   ^information unknown-goal
	   ^details <d>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get-goal
   	^details <d>)
}



sp {interpret-status*apply*unknown-goal*add*purpose
   (state <s> ^name interpret-status
   	      ^operator <o>)
   (<o> ^name get-goal
   	-^details)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-next-subaction
   	^satisfaction <sat>)
   (<sat> ^dialog-event <de1>)
   (<de1> ^originator instructor
   	  ^type goal-relation-message)
}

sp {interpret-status*apply*unknown-goal*add*purpose*details
   (state <s> ^name interpret-status
   	      ^operator <o>)
   (<o> ^name get-goal
   	^details <d>) 
   (<d> ^unknown attribute)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type get-goal
   	^reason <con>
   	^satisfaction <sat>)
   (<con> ^unknown attribute
   	  ^details <d>)
   (<sat> ^dialog-event <de1>)
   (<de1> ^originator instructor
   	  ^type goal-relation-message)
}


sp {interpret-status*apply*unknown-goal*message
   (state <s> ^name interpret-status
   	      ^operator <o>
	      ^topstate.interaction.stack.segment.purpose.satisfaction.action-event.type.verb.word <verb>)
   (<o> ^name get-goal)
--> 
   (<s> ^outgoing-message <msg>)  
   (<msg> ^type get-goal
   	  ^field.verb <verb>)
}


   	  
	      	