# For an index-object failure, where no objects are matches...
# This will cause the agent to ask for a teaching example

sp {interpret-status*propose*interpret-index-object-failure
   (state <s> ^name interpret-status
              ^status <status>
             -^handled <status>)
   (<status> ^type index-object-failure
             ^target-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-index-object-failure)
}

sp {interpret-status*apply*interpret-index-object-failure*mark*handled
   (state <s> ^name interpret-status
              ^operator.name interpret-index-object-failure
              ^status <status>)
-->
   (<s> ^handled <status>)
}

sp {interpret-status*apply*interpret-index-object-failure*create*new-segment
   (state <s> ^name interpret-status
              ^operator.name interpret-index-object-failure
              ^status.target-object <obj>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
        ^originator agent)
   (<p> ^type teaching-request
        ^target-object <obj>
        ^satisfaction.dialog-event <de>)
   (<de> ^type single-word-response
         ^originator instructor)
}

sp {interpret-status*apply*interpret-index-object-failure*create*message
   (state <s> ^name interpret-status
              ^operator.name interpret-index-object-failure
              ^status.target-object <obj>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type teaching-request
          ^target-object <obj>)
}

